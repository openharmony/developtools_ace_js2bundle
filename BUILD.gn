# Copyright (c) Huawei Technologies Co., Ltd. 2020-2020. All rights reserved.

import("//build/ohos.gni")
import("//build/ohos/ace/ace.gni")
import("//foundation/ace/ace_engine/ace_config.gni")

ace_loader_lib_dir =
    get_label_info(":build_ace_loader_library", "target_out_dir") + "/lib"

action("build_ace_loader_library") {
  script = "//foundation/ace/huawei_proprietary/tools/ace-loader/build_ace_loader_library.py"
  depfile = "$target_gen_dir/$target_name.d"
  outputs = [ ace_loader_lib_dir ]

  _ace_loader_dir =
      "//foundation/ace/huawei_proprietary/tools/ace-loader/ace-loader"
  _ace_toolkit_dir = "//prebuilts/ace-toolkit/ace-loader/linux-x64"

  _module_source_js = _ace_loader_dir + "/module-source.js"

  _babel_js = _ace_toolkit_dir + "/node_modules/@babel/cli/bin/babel.js"
  _babel_config_js = _ace_toolkit_dir + "/babel.config.js"
  _uglify_source_js = _ace_toolkit_dir + "/uglify-source.js"

  inputs = [
    _babel_config_js,
    _babel_js,
    _module_source_js,
    _uglify_source_js,
  ]

  # different host platform nodejs tool directory
  if (host_os == "linux") {
    nodejs_path =
        "//prebuilts/ace-toolkit/nodejs/node-v12.18.4-linux-x64/bin/node"
  } else if (host_os == "mac") {
    nodejs_path =
        "//prebuilts/ace-toolkit/nodejs/node-v12.18.4-darwin-x64/bin/node"
  } else {
    assert(false, "Unsupported host_os: $host_os")
  }

  args = [
    "--depfile",
    rebase_path(depfile, root_build_dir),
    "--node",
    rebase_path(nodejs_path, root_build_dir),
    "--babel-js",
    rebase_path(_babel_js, root_build_dir),
    "--weex-loader-src-dir",
    rebase_path(_ace_loader_dir + "/third_party/weex-loader/src",
                root_build_dir),
    "--ace-loader-src-dir",
    rebase_path(_ace_loader_dir + "/src", root_build_dir),
    "--babel-config-js",
    rebase_path(_babel_config_js, root_build_dir),
    "--module-source-js",
    rebase_path(_module_source_js, root_build_dir),
    "--uglify-source-js",
    rebase_path(_uglify_source_js, root_build_dir),
    "--output-dir",
    rebase_path(ace_loader_lib_dir, root_build_dir),
  ]
}

ohos_copy("ace_loader") {
  sources = [
    "ace-loader/.npmignore",
    "ace-loader/babel.config.js",
    "ace-loader/index.js",
    "ace-loader/main.product.js",
    "ace-loader/npm-install.js",
    "ace-loader/package-lock.json",
    "ace-loader/package.json",
    "ace-loader/router",
    "ace-loader/sample",
    "ace-loader/webpack.lite.config.js",
    "ace-loader/webpack.rich.config.js",
    "ace-loader/webpack.router.config.js",
  ]
  outputs = [ target_out_dir + "/$target_name/{{source_file_part}}" ]
  module_source_dir = target_out_dir + "/$target_name/"
  module_install_name = ""
  license_file = "//third_party/parse5/LICENSE"
}

ohos_copy("ace_loader_library") {
  deps = [ ":build_ace_loader_library" ]
  sources = [ ace_loader_lib_dir ]
  outputs = [ target_out_dir + "/$target_name" ]
  module_source_dir = target_out_dir + "/$target_name"
  module_install_name = ""
  license_file = "//third_party/weex-loader/NOTICE"
}
