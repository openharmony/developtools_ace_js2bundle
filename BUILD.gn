# Copyright (c) Huawei Technologies Co., Ltd. 2020-2020. All rights reserved.

import("//build/ohos.gni")
import("//build/ohos/ace/ace.gni")
import("//foundation/ace/ace_engine/ace_config.gni")

ace_loader_lib_dir =
    get_label_info(":build_ace_loader_library", "target_out_dir") + "/lib"

action("build_ace_loader_library") {
  script = "//developtools/ace-js2bundle/build_ace_loader_library.py"
  depfile = "$target_gen_dir/$target_name.d"
  outputs = [ ace_loader_lib_dir ]

  _ace_loader_dir = "//developtools/ace-js2bundle/ace-loader"

  _module_source_js = _ace_loader_dir + "/module-source.js"

  _babel_js = _ace_loader_dir + "/node_modules/@babel/cli/bin/babel.js"
  _babel_config_js = _ace_loader_dir + "/babel.config.js"
  _uglify_source_js = _ace_loader_dir + "/uglify-source.js"

  inputs = [
    _babel_config_js,
    _babel_js,
    _module_source_js,
    _uglify_source_js,
  ]

  # different host platform nodejs tool directory
  if (host_os == "linux") {
    nodejs_path =
        "//prebuilts/build-tools/common/nodejs/node-v12.18.4-linux-x64/bin/node"
  } else if (host_os == "mac") {
    nodejs_path = "//prebuilts/build-tools/common/nodejs/node-v12.18.4-darwin-x64/bin/node"
  } else {
    assert(false, "Unsupported host_os: $host_os")
  }

  args = [
    "--depfile",
    rebase_path(depfile, root_build_dir),
    "--node",
    rebase_path(nodejs_path, root_build_dir),
    "--babel-js",
    rebase_path(_babel_js, root_build_dir),
    "--weex-loader-src-dir",
    rebase_path("//third_party/weex-loader/src", root_build_dir),
    "--ace-loader-src-dir",
    rebase_path(_ace_loader_dir + "/src", root_build_dir),
    "--babel-config-js",
    rebase_path(_babel_config_js, root_build_dir),
    "--module-source-js",
    rebase_path(_module_source_js, root_build_dir),
    "--uglify-source-js",
    rebase_path(_uglify_source_js, root_build_dir),
    "--output-dir",
    rebase_path(ace_loader_lib_dir, root_build_dir),
  ]
}

ohos_copy("ace_loader") {
  sources = [
    "ace-loader/.npmignore",
    "ace-loader/babel.config.js",
    "ace-loader/index.js",
    "ace-loader/main.product.js",
    "ace-loader/npm-install.js",
    "ace-loader/package-lock.json",
    "ace-loader/package.json",
    "ace-loader/sample",
    "ace-loader/webpack.lite.config.js",
    "ace-loader/webpack.rich.config.js",
  ]
  outputs = [ target_out_dir + "/$target_name/{{source_file_part}}" ]
  module_source_dir = target_out_dir + "/$target_name/"
  module_install_name = ""
  license_file = "//third_party/parse5/LICENSE"
}

ohos_copy("ace_loader_library") {
  deps = [ ":build_ace_loader_library" ]
  sources = [ ace_loader_lib_dir ]
  outputs = [ target_out_dir + "/$target_name" ]
  module_source_dir = target_out_dir + "/$target_name"
  module_install_name = ""
  license_file = "//third_party/weex-loader/NOTICE"
}

# copy common_bin
ohos_copy("common_bin") {
  if (host_os == "mac") {
    sources =
        [ "//prebuilts/previewer/2.2.0.3/darwin/previewer/common/bin/bin" ]
  } else {
    sources =
        [ "//prebuilts/previewer/2.2.0.3/windows/previewer/common/bin/bin" ]
  }
  outputs = [ target_out_dir + "/common/bin/{{source_file_part}}" ]
  module_source_dir = target_out_dir + "/common/bin"
  module_install_name = ""
}

# copy liteWearable_bin
ohos_copy("liteWearable_bin") {
  if (host_os == "mac") {
    sources = [
      "//prebuilts/previewer/2.2.0.3/darwin/previewer/liteWearable/bin/bin",
    ]
  } else {
    sources = [
      "//prebuilts/previewer/2.2.0.3/windows/previewer/liteWearable/bin/bin",
    ]
  }
  outputs = [ target_out_dir + "/liteWearable/bin/{{source_file_part}}" ]
  module_source_dir = target_out_dir + "/liteWearable/bin"
  module_install_name = ""
}

# copy liteWearable_default
ohos_copy("liteWearable_default") {
  if (host_os == "mac") {
    sources = [ "//prebuilts/previewer/2.2.0.3/darwin/previewer/liteWearable/config/default/default" ]
  } else {
    sources = [ "//prebuilts/previewer/2.2.0.3/windows/previewer/liteWearable/config/default/default" ]
  }
  outputs =
      [ target_out_dir + "/liteWearable/config/default/{{source_file_part}}" ]
  module_source_dir = target_out_dir + "/liteWearable/config/default"
  module_install_name = ""
}

# copy liteWearable_images
ohos_copy("liteWearable_images") {
  if (host_os == "mac") {
    sources = [ "//prebuilts/previewer/2.2.0.3/darwin/previewer/liteWearable/config/images/images" ]
  } else {
    sources = [ "//prebuilts/previewer/2.2.0.3/windows/previewer/liteWearable/config/images/images" ]
  }
  outputs =
      [ target_out_dir + "/liteWearable/config/images/{{source_file_part}}" ]
  module_source_dir = target_out_dir + "/liteWearable/config/images"
  module_install_name = ""
}

# copy liteWearable_font
ohos_copy("liteWearable_font") {
  if (host_os == "mac") {
    sources = [
      "//prebuilts/previewer/2.2.0.3/darwin/previewer/liteWearable/config/RobotoCondensed-Regular.ttf",
      "//prebuilts/previewer/2.2.0.3/darwin/previewer/liteWearable/config/SourceHanSansSC-Regular.otf",
      "//prebuilts/previewer/2.2.0.3/darwin/previewer/liteWearable/config/font.bin",
      "//prebuilts/previewer/2.2.0.3/darwin/previewer/liteWearable/config/glyphs.bin",
      "//prebuilts/previewer/2.2.0.3/darwin/previewer/liteWearable/config/line_cj.brk",
    ]
  } else {
    sources = [
      "//prebuilts/previewer/2.2.0.3/windows/previewer/liteWearable/config/RobotoCondensed-Regular.ttf",
      "//prebuilts/previewer/2.2.0.3/windows/previewer/liteWearable/config/SourceHanSansSC-Regular.otf",
      "//prebuilts/previewer/2.2.0.3/windows/previewer/liteWearable/config/font.bin",
      "//prebuilts/previewer/2.2.0.3/windows/previewer/liteWearable/config/glyphs.bin",
      "//prebuilts/previewer/2.2.0.3/windows/previewer/liteWearable/config/line_cj.brk",
    ]
  }
  outputs = [ target_out_dir + "/liteWearable/config/{{source_file_part}}" ]
  module_source_dir = target_out_dir + "/liteWearable/config"
  module_install_name = ""
}
